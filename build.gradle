buildscript {
    repositories {
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.22.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.11.0'
        classpath 'com.palantir.gradle.docker:gradle-docker:0.27.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.104.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.10.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.15.0'
    id 'org.inferred.processors' version '3.6.0'
}

repositories {
    mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.external-publish'
apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.baseline-java-versions'

allprojects {
    apply plugin: 'com.palantir.java-format'
    version gitVersion()
    group 'com.palantir.config.crypto'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'maven-publish'
    apply plugin: 'com.palantir.baseline-class-uniqueness'
    apply plugin: 'com.palantir.baseline-exact-dependencies'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    tasks.check.dependsOn(checkImplicitDependencies)
    tasks.check.dependsOn(checkUnusedDependencies)
    tasks.check.dependsOn(javadoc)
}

javaVersions {
    libraryTarget = 11
    runtime = 17
}
