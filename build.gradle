buildscript {
    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-info-plugin:5.2.0'
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.1.2'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:6.0.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:14.1.1'
        classpath 'com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.3.0'
        classpath 'com.palantir.gradle.docker:gradle-docker:0.12.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.20.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.5.2'
    id 'org.inferred.processors' version '2.2.0'
    id 'com.palantir.jacoco-full-report' version '0.3.0'
    id 'org.unbroken-dome.test-sets' version '1.5.2'
}

repositories {
    jcenter()
    maven { url "http://palantir.bintray.com/releases" }
}

apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.circle.style'

dependencies {
    baseline 'com.palantir.baseline:gradle-baseline-java-config:0.20.1@zip'
}

allprojects {
    apply plugin: 'nebula.dependency-recommender'
    apply plugin: 'com.palantir.configuration-resolver'

    version gitVersion()
    group 'com.palantir.config.crypto'

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('version.props')
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-error-prone'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    javadoc {
        classpath = sourceSets.main.compileClasspath + configurations.processor
    }

    sourceCompatibility = 1.8

    jacocoTestReport {
        reports {
            xml.enabled = true
        }
        // Ignore Immutables-generated code from coverage checks
        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                // Exclusion path is project-relative and has a base of compiled classes
                fileTree(dir: it, exclude: '**/Immutable*.class')
            })
        }
    }
    tasks.jacocoTestReport.dependsOn(tasks.test)
    tasks.check.dependsOn(tasks.jacocoTestReport)
    tasks.check.dependsOn(javadoc)

    if (System.env.CIRCLECI) {
        test.reports.junitXml.destination = new File(System.env.TESTS_DIR, it.getName())
    }

    compileJava {
        options.compilerArgs += [
            '-XepDisableWarningsInGeneratedCode',
            '-Xep:OptionalEquality:WARN',
            '-Werror' 
        ]
    }
}
